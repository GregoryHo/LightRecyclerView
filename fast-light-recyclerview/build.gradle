apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// Library version
version = "1.0.3.s22"
android {
  compileSdkVersion 22
  buildToolsVersion "25.0.3"

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 22
    versionCode 1
    versionName version
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

// Home page url
def siteUrl = 'https://github.com/GregoryHo/FastAndLightRecyclerView'
// git repository url
def gitUrl = 'https://github.com/GregoryHo/FastAndLightRecyclerView.git'
// Maven Group ID for the artifactï¼Œ
group = "greg.ns.library"
install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'
        // Add your description here
        name 'A module of recyclerview'
        url siteUrl
        // Set your license
        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id 'Gregory'
            name 'Gregory'
            email 'greghojob@gmail.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}
task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  configurations = ['archives']
  pkg {
    repo = "maven"
    name = "fast-light-recyclerview"
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ["MIT"]
    publish = true
  }
}

dependencies {
  final SUPPORT_VERSION = '22.2.1'

  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
  compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
}
